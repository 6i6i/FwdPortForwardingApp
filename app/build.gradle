apply plugin: 'com.android.application'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.0"

    def versionPropsFile = file('version.properties')

    /*
    Version numbering from: http://andreborud.com/android-studio-automatic-incremental-gradle-versioning/
     */
    if (versionPropsFile.canRead()) {
        def Properties versionProps = new Properties()

        versionProps.load(new FileInputStream(versionPropsFile))
        def value = 0
        def runTasks = gradle.startParameter.taskNames
        if ('assemble' in runTasks || 'assembleRelease' in runTasks || 'aR' in runTasks || 'generateReleaseBuildConfig' in runTasks) {
            value = 1;
        }

        def versionMajor = 1
        def versionMinor = 4
        def versionPatch = versionProps['VERSION_PATCH'].toInteger() + value
        def versionBuild = versionProps['VERSION_BUILD'].toInteger() + 1
        def version_Code = versionProps['VERSION_CODE'].toInteger() + value

        versionProps['VERSION_PATCH'] = versionPatch.toString()
        versionProps['VERSION_BUILD'] = versionBuild.toString()
        versionProps['VERSION_CODE'] = version_Code.toString()

        versionProps.store(versionPropsFile.newWriter(), null)

        defaultConfig {
            applicationId "com.elixsr.portforwarder"
            minSdkVersion 16
            targetSdkVersion 25
            versionCode version_Code
            versionName "${versionMajor}.${versionMinor}.${versionPatch} (${versionBuild})"
            multiDexEnabled true
        }
    } else {
        throw new GradleException("Could not read version.properties!")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.0'
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:26.0.0+'
    compile 'com.android.support:design:26.0.0+'
    compile 'com.android.support:percent:26.0.0+'
    compile 'com.android.support:recyclerview-v7:26.0.0+'
    compile 'com.elixsr:elixsr-core:0.1.8'
    compile 'com.github.paolorotolo:appintro:4.0.0'
    compile 'com.google.android.gms:play-services-analytics:9.2.1'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    testCompile 'junit:junit:4.12'
    implementation 'com.google.firebase:firebase-crash:9.2.1'
}

apply plugin: 'com.google.gms.google-services'